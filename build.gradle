apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'spring-boot'

buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

jar {
	baseName = 'ReportManagementSystem'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

javadoc {
//	destinationDir   = new File("${projectDir}\\docs\\javadoc")
	options.charSet  = 'UTF-8'
	options.encoding = 'UTF-8'
}

// DomaのSQLファイルをコンパイルより前に出力先Dirにコピーするための設定
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

// doma-gen用
configurations {
	domaGenRuntime
}

dependencies {
	// 本体
	compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
	compile group: 'org.seasar.doma.boot', name: 'doma-spring-boot-starter', version: '1.+'
	compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4', version: '2.+'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '+'
	runtime group: 'mysql', name: 'mysql-connector-java'

	// サーバ状態監視用
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
	compile group: 'org.springframework.boot', name: 'spring-boot-actuator-docs'

	// test用
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
	testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.+'

	// generator用
	compile group: 'org.freemarker', name: 'freemarker', version: '+'

	// doma-gen用
	domaGenRuntime group: 'org.seasar.doma', name: 'doma-gen', version: '+'
	domaGenRuntime group: 'mysql', name: 'mysql-connector-java'
}

apply from:'eclipse.gradle'
apply from:'doma.gradle'
